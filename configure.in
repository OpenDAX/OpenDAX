AC_PREREQ(2.59)
AC_INIT(dax/opendax.c)
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)

VERSION="0.2"
PACKAGE=opendax

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
#AC_DEFINE_UNQUOTED(SYSCONFDIR,"${sysconfdir}")
#AC_DEFINE_UNQUOTED([ETC_DIR],["${sysconfdir}/$PACKAGE"],"ETC_DIR")

CFLAGS=" "

AC_PROG_CC
#AC_PROG_RANLIB
AC_PROG_LIBTOOL

AC_ARG_ENABLE([debug],[  --enable-debug    Enable debugging information],
              USE_DEBUG="yes", USE_DEBUG="no")


# These lines are used for the conditional compilation of different modules
AC_ARG_ENABLE([modbus], 
[  --disable-modbus   Do Not Compile Modbus Module], 
[case "${enableval}" in 
    yes) modbus=true ;; 
    no) modbus=false ;;
    *) AC_MSG_ERROR([bad value ${enableval} for --enable-modbus]) ;; 
    esac],[modbus=true]) 
AM_CONDITIONAL([MODBUS], [test x$modbus = xtrue])

AC_ARG_ENABLE([test], 
[  --disable-test   Do Not Compile Test Module], 
[case "${enableval}" in 
    yes) test=true ;; 
    no) test=false ;;
    *) AC_MSG_ERROR([bad value ${enableval} for --enable-test]) ;; 
    esac],[test=true]) 
AM_CONDITIONAL([TEST], [test x$test = xtrue])

# Checks for libraries.
# Not all libraries go in all modules so we save the old ones here
# so that we can overwrite them later.
OLD_LIBS=$LIBS

#These lines will check for the library and add the right stuff to config.h
AC_CHECK_LIB([pthread], [pthread_create], [got_pthread=true])
AC_CHECK_LIB([readline], [readline], [got_readline=true])
AC_CHECK_LIB([lua],[luaL_newstate], [got_lua=true], [got_lua=false])
AC_CHECK_LIB([lua50],[luaL_newstate], [got_lua50=true], [got_lua50=false])
AC_CHECK_LIB([lua5.1],[luaL_newstate], [got_lua5_1=true], [got_lua5_1=false])
AC_CHECK_LIB([lua51],[luaL_newstate], [got_lua51=true], [got_lua51=false])

#Replace what AC_CHECK_LIB does to the LIBS variable
LIBS=$OLD_LIBS

#Some Automake conditionals that we can use later
AM_CONDITIONAL([HAVE_PTHREAD], test $got_pthread = true)
AM_CONDITIONAL([HAVE_READLINE], test $got_readline = true)
AM_CONDITIONAL([HAVE_LUA], test $got_lua = true)
AM_CONDITIONAL([HAVE_LUA51], test $got_lua51 = true)
AM_CONDITIONAL([HAVE_LUA5_1], test $got_lua5_1 = true)

if test $got_lua = true ; then
  LUALIB='-llua'
elif test $got_lua51 = true ; then
  LUALIB='-llua51'
elif test $got_lua5_1 = true ; then
  LUALIB='-llua5.1'
else
  LUALIB=
fi

AC_SUBST(LUALIB)

# Checks for header files.
#AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([arpa/inet.h fcntl.h netinet/in.h stdlib.h string.h sys/socket.h sys/ioctl.h sys/param.h sys/time.h syslog.h termios.h unistd.h readline/readline.h signal.h])

AC_CHECK_HEADERS([lua5.1/lua.h lua5.1/lauxlib.h lua5.1/lualib.h])
AC_CHECK_HEADERS([lua51/lua.h lua51/lauxlib.h lua51/lualib.h])
AC_CHECK_HEADERS([lua/lua.h lua/lauxlib.h lua/lualib.h])
AC_CHECK_HEADERS([lua.h lauxlib.h lualib.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([dup2 gettimeofday memmove memset socket strcasecmp strdup strerror strpbrk strtol strtoul])

CFLAGS="$CFLAGS -Wall -DETC_DIR=\\\"$sysconfdir/$PACKAGE\\\"   "

if test $USE_DEBUG = yes ; then
#   DEBUG=1
   CFLAGS="$CFLAGS -DDEBUG=1 -g"
else
#   DEBUG=0
   CFLAGS="$CFLAGS -O3"
fi



AC_SUBST(ac_aux_dir)

#I'll use this once I start on the hmi module.  It's different enough that it justifies it's own conigure.in
#AC_CONFIG_SUBDIRS([modules/hmi])

AC_CONFIG_FILES([Makefile
                 dax/Makefile
                 lib/Makefile
                 modules/Makefile
                 modules/modbus/Makefile
                 modules/daxc/Makefile
                 modules/daxlua/Makefile
                 modules/test/Makefile])
AC_OUTPUT
